import MIL.Common
import Mathlib.Topology.Instances.Real
import Mathlib.Analysis.Normed.Operator.BanachSteinhaus
import Mathlib.Data.Real.Irrational

open Set Filter Topology

-- 10.3. Topological spaces

-- 10.3.1. Fundamentals

-- There is no need to rely on a metric to define a topology.

-- First point of view: using open sets

-- A topological space is a set X and a topology on X.
-- A topology on X is a collection of subsets of X, called open sets, s.t.
--   1. The empty set and X are open.
--   2. The union of any collection of open sets is open.
--   3. The intersection of any finite collection of open sets is open.

-- Intuition: an open set contains a neighborhood of each of its points.
-- Intuition: an open set is a neighborhood around each of its points.

section

-- Let X be a topological space.
variable {X : Type*} [TopologicalSpace X]

-- X is open.
-- Intuition: X contains all neighborhoods,
--            including all neighborhoods of its points.
example : IsOpen (univ : Set X) :=
  isOpen_univ

-- The empty set âˆ… is open.
-- Intuition: the empty set contains no points,
--            so it trivially contains all neighborhoods of its points.
example : IsOpen (âˆ… : Set X) :=
  isOpen_empty

-- The union of any collection of open sets is open.
-- Note that there is no restriction on the index set Î¹.
-- Intuition: the union of neighborhoods is a neighborhood.
example {Î¹ : Type*} {s : Î¹ â†’ Set X} (hs : âˆ€ i, IsOpen (s i)) : IsOpen (â‹ƒ i, s i) :=
  isOpen_iUnion hs

-- The intersection of any finite collection of open sets is open.
-- Note that the index set Î¹ is finite.
-- Intuition: finite intersection of neighborhoods is a neighborhood.
--            however, if there are infinitely many neighborhoods,
--            the intersection may "zoom in" too much and not be a neighborhood.
example {Î¹ : Type*} [Fintype Î¹] {s : Î¹ â†’ Set X} (hs : âˆ€ i, IsOpen (s i)) :
    IsOpen (â‹‚ i, s i) :=
  isOpen_iInter_of_finite hs

-- Example: â‹‚â‚™ (-1/(n+1), 1/(n+1)) is closed
-- Proof: the intersection is {0}, which is closed.
example : IsClosed (â‹‚ n : â„•, Ioo (-(1 : â„) / (n + 1)) ((1 : â„) / (n + 1))) := by
  -- Show â‹‚â‚™ (-1/(n+1), 1/(n+1)) = {0}.
  have : (â‹‚ n : â„•, Ioo (-(1 : â„) / (n + 1)) ((1 : â„) / (n + 1))) = {0} := by
    ext x
    constructor
    Â· -- Suppose x âˆˆ â‹‚â‚™ (-1/(n+1), 1/(n+1)).
      -- Show x = 0.
      intro h
      -- Then, âˆ€ n âˆˆ â„•, x âˆˆ (-1/(n+1)) (1/(n+1)).
      rw [mem_iInter] at h
      -- Suppose for contradiction x â‰  0.
      by_contra! hxne0
      by_cases hx : 0 < x
      Â· -- Case 1: x > 0.
        -- Then, âˆƒ n âˆˆ â„•, x > 1/(n+1).
        rcases exists_nat_one_div_lt hx with âŸ¨n, hxnâŸ©
        -- Fix such n. We have x âˆˆ (-1/(n+1), 1/(n+1)).
        have := h n
        -- This is a contradiction.
        exact not_lt_of_le (le_of_lt this.right) hxn
      Â· -- Case 2: x < 0.
        push_neg at hx
        have hx := lt_of_le_of_ne hx hxne0
        -- Then, âˆƒ n âˆˆ â„•, x < -1/(n+1).
        rcases exists_nat_one_div_lt (neg_pos.mpr hx) with âŸ¨n, hxnâŸ©
        -- Fix such n. We have x âˆˆ (-1/(n+1), 1/(n+1)).
        have := h n
        -- This is a contradiction.
        have := this.left
        rw [neg_div, neg_lt] at this
        exact not_lt_of_lt hxn this
    Â· -- Suppose x = 0.
      -- Show x âˆˆ â‹‚ n, (-1/(n+1), 1/(n+1)).
      intro h
      -- Show âˆ€ n âˆˆ â„•, x âˆˆ (-1/(n+1), 1/(n+1)).
      rw [mem_iInter]
      -- Let n âˆˆ â„•.
      -- show x âˆˆ (-1/(n+1), 1/(n+1)).
      intro n
      -- Show -1/(n+1) < x < 1/(n+1).
      rw [mem_Ioo]
      constructor
      Â· -- Show -1/(n+1) < x.
        -- Since x = 0, this is trivial.
        rw [h]
        rw [neg_div]
        exact neg_neg_of_pos Nat.one_div_pos_of_nat
      Â· -- Show x < 1/(n+1).
        -- Since x = 0, this is trivial.
        rw [h]
        exact Nat.one_div_pos_of_nat
  -- The intersection is {0}, which is closed.
  rw [this]
  exact isClosed_singleton

-- Some view this collection of axioms as redundant:
--   The empty set is open because it is the union of no open sets.
--   The universe is open because it is the intersection of no open sets.

-- Let Y be a topological space.
variable {Y : Type*} [TopologicalSpace Y]

-- A set is closed if its complement is open.
example {S : Set X} : IsOpen Sá¶œ â†” IsClosed S := isOpen_compl_iff

-- A function between two topological spaces is continuous
-- if the preimage of every open set in the codomain is open in the domain.
-- Intuition: continuous function preserves "neighborhoods",
--            i.e., points that are close together in the domain
--                  are mapped to points that are close together in the codomain.
example {f : X â†’ Y} : Continuous f â†” âˆ€ s, IsOpen s â†’ IsOpen (f â»Â¹' s) :=
  continuous_def

-- A topological structure determines which functions (with domain
-- and codomain in the topological space) are continuous.
-- Two topological structures on the same set are the same
-- if they have the same continuous functions.
-- The identity function is used as a witness of equivalence:
--   if the identity function is continuous in both directions,
--   then an open set in one topology has to be open in the other topology,
--   thus the two topologies are the same.

-- Second point of view: using neighborhoods

-- A topological space is a set equipped with
-- a neighborhood filter ğ“(x) attached to each point x,
-- i.e., there is a neighborhood function mapping each point x to a filter ğ“(x)
--       called the neighborhood of x.
-- Two roles of this neighborhood filter (as previously studied):
-- 1. ğ“(x) is the generalized set of points of X close to x.
-- 2. ğ“(x) gives a way to say a predicate `P : X â†’ Prop` holds
--    for points close enough to x.

-- Motivation: easier to talk about continuity at a point

-- Continuity at a point:

-- Filter
-- A function f : X â†’ Y is continuous at x if
-- the direct image under f of the generalized set of points close to x
-- is contained in the generalized set of points close to f(x).
-- Formally, f(ğ“(x)) â‰¤ ğ“(f(x))
-- i.e., ğ“(f(x)) âŠ† f(ğ“(x)) (not expressible in Lean)
-- i.e., âˆ€ V âˆˆ ğ“(f(x)), V âˆˆ f(ğ“(x))
-- i.e., âˆ€ V âˆˆ ğ“(f(x)), fâ»Â¹(V) âˆˆ ğ“(x)
-- i.e., âˆ€ V âˆˆ ğ“(f(x)), âˆƒ U âˆˆ ğ“(x), f(U) = V
-- Note: it does not mean any neighborhood of x is mapped to a neighborhood of f(x)
--       more like there exists a neighborhood of x mapped to any neighborhood of f(x)
example {f : X â†’ Y} {x : X} : ContinuousAt f x â†” map f (ğ“ x) â‰¤ ğ“ (f x) :=
  Iff.rfl

-- Ordinary set
-- A function f is continuous at x iff
-- for every neighborhood U of f(x), all points sufficiently close to x
-- are sent to U.
example {f : X â†’ Y} {x : X} : ContinuousAt f x â†” âˆ€ U âˆˆ ğ“ (f x), âˆ€á¶  x in ğ“ x, f x âˆˆ U :=
  Iff.rfl

-- Recall there are two points of view: open sets and neighborhood filters
-- We can go between two points of view.

-- From open sets to neighborhood filters:
-- Constructing neighborhood filters from open sets:
-- Given a point x in a topological space X equipped with a topology,
-- the neighborhood filter is defined as follows:
-- A set S âˆˆ ğ“(x) (S is a neighborhood of x) iff
-- S contains an open set containing x.
example {x : X} {s : Set X} : s âˆˆ ğ“ x â†” âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ x âˆˆ t :=
  mem_nhds_iff

-- From neighborhood filters to open sets:
-- The function `ğ“ : X â†’ Filter X` needs to satisfy some properties
-- to define a topology on X.

-- The `pure x` filter:
--   generated by {x}
--   containing all sets containing x
example {x : X} : ğ“Ÿ {x} = pure x := principal_singleton x

-- All subsets in ğ“Ÿ({x}) contains x
-- since each of them contains {x} as a subset.
example {x : X} {S : Set X} : S âˆˆ ğ“Ÿ {x} â†’ x âˆˆ S := fun hSPx â†¦ hSPx rfl

-- Property 1: a point is close to itself
--   ğ“(x) contains {x}
--   `pure x` â‰¤ ğ“(x)
example (x : X) : pure x â‰¤ ğ“ x :=
  pure_le_nhds x

-- Any property that holds for points close enough to x also holds for x itself.
-- Given a point x âˆˆ X and a proposition P(â¬) on X
-- If P(y) holds for y sufficiently close to x
-- then P(x) holds
example (x : X) (P : X â†’ Prop) (h : âˆ€á¶  y in ğ“ x, P y) : P x :=
  h.self_of_nhds

-- Property 2: nearness is transitive
-- Given a point x âˆˆ X and a proposition P(Â·) on X
-- If P(y) holds for y sufficiently close to x
-- then for y sufficiently close to x and z sufficiently close to y, P(z) holds.
-- Intuition: if y is in a neighborhood of x and z is in a neighborhood of y,
--            then z is in a (potentially larger) neighborhood of x.
-- Note: this actually goes both way
example {P : X â†’ Prop} {x : X} (h : âˆ€á¶  y in ğ“ x, P y) : âˆ€á¶  y in ğ“ x, âˆ€á¶  z in ğ“ y, P z :=
  eventually_eventually_nhds.mpr h

-- Construct a topological space from a neighborhood function
-- However it only produces a topology where the neighborhood function
-- satisfies the two properties.
#check TopologicalSpace.mkOfNhds

-- If a neighborhood function satisfies two properties
-- then it is the same as the "true" neighborhood function.
#check TopologicalSpace.nhds_mkOfNhds

-- A neighborhood function (mapping each point to a filter)
-- satisfying the two properties above will also satisfy:
-- for any point x âˆˆ X and any neighborhood S of x
-- there exists a neighborhood T âŠ† S such that any point x' âˆˆ T
-- S is also a neighborhood of x'.
-- Given:
--   neighborhood function n : X â†’ Filter X
-- If:
--   (property 1) âˆ€ x âˆˆ X, ğ“Ÿ({x}) â‰¤ n(x)
--   (property 2) âˆ€ x âˆˆ X, âˆ€ proposition P(Â·) on X,
--                if âˆ€á¶  y âˆˆ n(x), P(y) holds then âˆ€á¶  y âˆˆ n(x), âˆ€á¶  z âˆˆ n(y), P(z) holds
-- Then
--   âˆ€ x âˆˆ X, âˆ€ S âˆˆ n(x), âˆƒ T âˆˆ n(x) s.t. T âŠ† S and âˆ€ x' âˆˆ T, S âˆˆ n(x')
example {X : Type*} (n : X â†’ Filter X) (Hâ‚ : âˆ€ x, pure x â‰¤ n x)
    (Hâ‚‚ : âˆ€ x : X, âˆ€ P : X â†’ Prop, (âˆ€á¶  y in n x, P y) â†’ âˆ€á¶  y in n x, âˆ€á¶  z in n y, P z) :
    âˆ€ x, âˆ€ S âˆˆ n x, âˆƒ T âˆˆ n x, T âŠ† S âˆ§ âˆ€ y âˆˆ T, S âˆˆ n y := by
  -- Let x âˆˆ X, S âŠ† X s.t. S âˆˆ n(x).
  -- Show: âˆƒ T âˆˆ n(x) s.t. T âŠ† S and âˆ€ x' âˆˆ T, S âˆˆ n(x')
  intro x S hSnx
  -- Let T = {x | S âˆˆ n(x)}
  -- i.e., T contains all points x having S as its neighborhood.
  use {y | S âˆˆ n y}
  -- Show T âˆˆ n(x)
  have hTâ‚ : {y | S âˆˆ n y} âˆˆ n x :=
    -- In Lean, we can just write:
    -- `Hâ‚‚ x S hSnx`
    -- but that is not clear to me.
    by
    -- Since S âˆˆ n(x), âˆ€á¶  y âˆˆ n(x), y âˆˆ S, read,
    -- for y sufficiently close to x, y âˆˆ S.
    have : S âˆˆ n x â†” (âˆ€á¶  y in n x, y âˆˆ S) := Iff.rfl
    rw [this] at hSnx
    -- The goal of T âˆˆ n(x) can be equivalently written as
    -- âˆ€á¶  y âˆˆ n x, S âˆˆ n(y), read,
    -- for y sufficiently close to x, S âˆˆ n(y)
    -- or, equivalently,
    -- âˆ€á¶  y âˆˆ n x, âˆ€á¶  z âˆˆ n y, z âˆˆ S, read,
    -- for y sufficiently close to x, for z sufficiently close to y, z âˆˆ S.
    have : ({y | S âˆˆ n y} âˆˆ n x) â†” (âˆ€á¶  y in n x, âˆ€á¶  z in n y, z âˆˆ S) := eventually_iff
    rw [this]
    -- We apply the "transitivity of nearness" property with x and P(â¬) = â¬ âˆˆ S.
    exact Hâ‚‚ x S hSnx
  -- Show T âŠ† S
  have hTâ‚‚ : {y | S âˆˆ n y} âŠ† S :=
    -- Let y âˆˆ T. Then, S âˆˆ n(y).
    -- Since n(y) âŠ† ğ“Ÿ({y}), S âˆˆ ğ“Ÿ({y}).
    -- Thus, y âˆˆ S.
    fun y hSny â†¦ Hâ‚ y hSny
  -- Show âˆ€ x' âˆˆ T, S âˆˆ n(x')
  have hTâ‚ƒ : âˆ€ y âˆˆ {y | S âˆˆ n y}, S âˆˆ n y :=
    -- Let y âˆˆ T. Then, S âˆˆ n(y).
    fun y a â†¦ a
  -- We complete the proof.
  exact âŸ¨hTâ‚, hTâ‚‚, hTâ‚ƒâŸ©

end

-- Let X and Y be two spaces.
variable {X Y : Type*}

-- Comparing between topological spaces and metric spaces

-- Functoriality: ability to construct new structures from existing ones
-- for spaces, define structures on subsets, quotients, products, etc.

-- Metric space has limited functoriality
-- A metric space structure can be induced on a subset or,
-- equivalently, it can be pulled back by an injective map.

-- Inducing a metric space on a subset
-- Let (X, d) be a metric space. Let A âŠ† X.
-- Then, (A, dA) where dA(aâ‚, aâ‚‚) = d(aâ‚, aâ‚‚) âˆ€ aâ‚, aâ‚‚ âˆˆ A, is a metric space.
def SubsetMetricSpace {X : Type*} [MetricSpace X] (A : Set X) :
  MetricSpace A where
  eq_of_dist_eq_zero := by
    intro aâ‚ aâ‚‚ h
    exact eq_of_dist_eq_zero h

-- Inducing a metric space by pulling back an injective map
-- Let f : A â†’ X be an injective map from a set A to a metric space (X, d).
-- Then, (A, df) where df(aâ‚, aâ‚‚) = d(f(aâ‚), f(aâ‚‚)) âˆ€ aâ‚, aâ‚‚ âˆˆ A, is a metric space.
-- Injective is necessary to ensure df(aâ‚, aâ‚‚) = 0 â†’ aâ‚ = aâ‚‚.
def PullbackMetricSpace {A X : Type*} (hX: MetricSpace X) (f : A â†’ X) (hf : Function.Injective f)
  : MetricSpace A where
  dist := fun aâ‚ aâ‚‚ => hX.dist (f aâ‚) (f aâ‚‚)
  dist_self := fun a => hX.dist_self (f a)
  dist_comm := fun aâ‚ aâ‚‚ => hX.dist_comm (f aâ‚) (f aâ‚‚)
  dist_triangle := fun aâ‚ aâ‚‚ aâ‚ƒ => hX.dist_triangle (f aâ‚) (f aâ‚‚) (f aâ‚ƒ)
  eq_of_dist_eq_zero := by
    intro aâ‚ aâ‚‚ h
    exact hf (hX.eq_of_dist_eq_zero h)

-- Inducing a metric space on a subset is pulling back by the inclusion map.
def SubsetMetricSpace' {X : Type*} [hX: MetricSpace X] (A : Set X) : MetricSpace A :=
  PullbackMetricSpace hX ((â†‘) : A â†’ X) Subtype.coe_injective

-- The two metric space structures are the same.
example {X : Type*} [MetricSpace X] (A : Set X)
  : SubsetMetricSpace A = SubsetMetricSpace' A :=
  MetricSpace.ext rfl

-- Pulling back a metric space by an injective map is inducing a metric space on the image.
def PullbackMetricSpace' {A X : Type*} (hX : MetricSpace X) (f : A â†’ X) (hf : Function.Injective f)
  : MetricSpace A where
  dist := fun aâ‚ aâ‚‚ â†¦
    (SubsetMetricSpace (Set.range f)).dist (rangeFactorization f aâ‚) (rangeFactorization f aâ‚‚)
  dist_self := fun x â†¦
    hX.dist_self (rangeFactorization f x)
  dist_comm := fun x y â†¦
    hX.dist_comm (rangeFactorization f x) (rangeFactorization f y)
  dist_triangle := fun x y z â†¦
    hX.dist_triangle (rangeFactorization f x) (rangeFactorization f y) (rangeFactorization f z)
  eq_of_dist_eq_zero := by
    intro aâ‚ aâ‚‚ h
    exact hf (hX.eq_of_dist_eq_zero h)

-- The two metric space structures are the same.
example {A X : Type*} (hX : MetricSpace X) (f : A â†’ X) (hf : Function.Injective f)
  : PullbackMetricSpace hX f hf = PullbackMetricSpace' hX f hf :=
  MetricSpace.ext rfl

-- But that is pretty much everything for metric spaces.

-- For topological spaces, arbitrary functions can induce a new topology.

-- Given f : X â†’ Y, a topology T_X on X can be pushed forward to a topology on Y,
-- denoted by f_* T_X.
example (f : X â†’ Y) : TopologicalSpace X â†’ TopologicalSpace Y :=
  TopologicalSpace.coinduced f

-- V is open in (Y, T_Y) if its preimage is open in (X, T_X).
def PushforwardTopologicalSpace {X Y : Type*} (T_X : TopologicalSpace X) (f : X â†’ Y)
  : TopologicalSpace Y where
  IsOpen V := T_X.IsOpen (f â»Â¹' V)
  isOpen_univ := T_X.isOpen_univ
  isOpen_inter V V' := T_X.isOpen_inter (f â»Â¹' V) (f â»Â¹' V')
  isOpen_sUnion S := by
    intro hS
    rw [preimage_sUnion]
    exact isOpen_biUnion hS

example [hX : TopologicalSpace X] (f : X â†’ Y) (V : Set Y)
  : (hX.coinduced f).IsOpen V â†” hX.IsOpen (f â»Â¹' V)
  := isOpen_coinduced

-- Given f : X â†’ Y, a topology T_Y on Y can be pulled back to a topology on X,
-- denoted by f^* T_Y.
example (f : X â†’ Y) : TopologicalSpace Y â†’ TopologicalSpace X :=
  TopologicalSpace.induced f

-- U is open in (X, T_X) iff U is the preimage of some open set in (Y, T_Y).
def PullbackTopologicalSpace {X Y : Type*} (T_Y : TopologicalSpace Y) (f : X â†’ Y)
  : TopologicalSpace X where
  IsOpen := fun U => âˆƒ (V : Set Y), T_Y.IsOpen V âˆ§ f â»Â¹' V = U
  isOpen_univ := by
    use univ
    exact âŸ¨T_Y.isOpen_univ, rflâŸ©
  isOpen_inter U U' := by
    rintro âŸ¨V, hV, rflâŸ© âŸ¨V', hV', rflâŸ©
    use V âˆ© V'
    exact âŸ¨T_Y.isOpen_inter V V' hV hV', rflâŸ©
  isOpen_sUnion S := by
    intro hS
    choose! g hâ‚ hâ‚‚ using hS
    use â‹ƒâ‚€ (g '' S)
    constructor
    Â· apply T_Y.isOpen_sUnion
      rintro _ âŸ¨U, hU, rflâŸ©
      exact hâ‚ U hU
    Â· rw [preimage_sUnion, biUnion_image, sUnion_eq_biUnion]
      exact iUnionâ‚‚_congr hâ‚‚

example [hY : TopologicalSpace Y] (f : X â†’ Y) (U : Set X)
  : (hY.induced f).IsOpen U â†” âˆƒ (V : Set Y), hY.IsOpen V âˆ§ f â»Â¹' V = U
  := isOpen_induced_iff

-- Push and pull operations form a Galois connection.
--   f_* T_X â‰¤ T_Y â†” T_X â‰¤ f^* T_Y
example (f : X â†’ Y) (T_X : TopologicalSpace X) (T_Y : TopologicalSpace Y) :
    TopologicalSpace.coinduced f T_X â‰¤ T_Y â†” T_X â‰¤ TopologicalSpace.induced f T_Y :=
  coinduced_le_iff_le_induced

-- Given f : X â†’ Y and g : Y â†’ Z, (g âˆ˜ f)_* T_X = g_* (f_* T_X).
-- (pushing forward is covariant)
#check coinduced_compose

-- Given f : X â†’ Y and g : Y â†’ Z, (g âˆ˜ f)^* T_Z = f^* (g^* T_Z).
-- (pulling back is contravariant)
#check induced_compose

-- Order on topologies
-- Want:
--   f(T) = ğ“_T(x) is order preserving
--   i.e., âˆ€ x âˆˆ X, if T â‰¤ T' then ğ“_T(x) â‰¤ ğ“_T'(x)
--   i.e., every neighborhood of a point according to T'
--         is a neighborhood of that point according to T
--         but there may be more neighborhoods of the point according to T
--   Intuition: T' has a stricter requirement regarding neighborhoods of a point
-- Therefore:
--   T â‰¤ T' if any open V in T' is open in T
example {T T' : TopologicalSpace X} : T â‰¤ T' â†” âˆ€ s, T'.IsOpen s â†’ T.IsOpen s :=
  Iff.rfl

-- T â‰¤ T' iff for all X, ğ“_T(x) â‰¤ ğ“_T'(x)
-- reads as "T is finer than T'"
example {T T' : TopologicalSpace X} : T â‰¤ T' â†” âˆ€ x, @nhds X T x â‰¤ @nhds X T' x :=
  le_iff_nhds T T'

-- f is continuous wrt T_X and T_Y iff f_* T_X â‰¤ T_Y
-- Note: f_* T_X is the finest/"smallest"/"has the most sets" topology on Y making f continuous
example (T_X : TopologicalSpace X) (T_Y : TopologicalSpace Y) (f : X â†’ Y) :
    Continuous f â†” TopologicalSpace.coinduced f T_X â‰¤ T_Y :=
  continuous_iff_coinduced_le

-- f is continuous wrt T_X and T_Y iff T_X â‰¤ f^* T_Y
-- f^* T_Y is the coarsest/"largest"/"has the fewest sets" topology on X making f continuous
-- (related to the Galois connection: f_* T_X â‰¤ T_Y â†” T_X â‰¤ f^* T_Y)
example (T_X : TopologicalSpace X) (T_Y : TopologicalSpace Y) (f : X â†’ Y) :
    Continuous f â†” T_X â‰¤ TopologicalSpace.induced f T_Y :=
    continuous_iff_coinduced_le.trans coinduced_le_iff_le_induced
    -- or, `continuous_iff_le_induced`

-- Universal property of the pushforward topology:
-- Given:
--   f : X â†’ Y, g : Y â†’ Z
--   topology T_X on X
-- Then:
--   for every topology T_Z on Z,
--   g is continuous wrt f_* T_X and T_Z iff
--   g âˆ˜ f is continuous wrt T_X and T_Z
-- Proof: g is continuous â†” g_* (f_* T_X) â‰¤ T_Z â†” (g âˆ˜ f)_* T_X â‰¤ T_Z â†” g âˆ˜ f is continuous
example {Z : Type*} (f : X â†’ Y) (g : Y â†’ Z) (T_X : TopologicalSpace X) (T_Z : TopologicalSpace Z)
  : Continuous[T_X.coinduced f, T_Z] g â†” Continuous[T_X, T_Z] (g âˆ˜ f) := by
  rw [continuous_iff_coinduced_le, coinduced_compose, continuous_iff_coinduced_le]
  -- or, `continuous_coinduced_dom`

-- Universal property of the pullback topology:
-- Given:
--   g : X â†’ Y, g : Y â†’ Z
--   topology T_Z on Z
-- Then:
--   for every topology T_X on X,
--   f is continuous wrt T_X and g^* T_Z iff
--   g âˆ˜ f is continuous wrt T_X and T_Z
-- Proof: f is continuous â†” T_X â‰¤ f^*(g^* T_Z) â†” T_X â‰¤ (g âˆ˜ f)^* T_Z â†” g âˆ˜ f is continuous
example {Z : Type*} (f : X â†’ Y) (g : Y â†’ Z) (T_X : TopologicalSpace X) (T_Z : TopologicalSpace Z)
  : Continuous[T_X, T_Z.induced g] f â†” Continuous[T_X, T_Z] (g âˆ˜ f) := by
  rw [continuous_iff_le_induced, induced_compose, continuous_iff_le_induced]
  -- or, `continuous_induced_rng`

-- Given a topological space (X, T) and an equivalence relation ~ on X,
-- the quotient space X / ~ is a topological space with topology f_* T,
-- where f : X â†’ X / ~ is the projection map.
-- Intuition: the topology on the quotient space is the finest topology
--            making the projection map continuous
instance QuotientTopologicalSpace (T : TopologicalSpace X) (S : Setoid X) :
    TopologicalSpace (Quotient S) := T.coinduced (Quotient.mk S)

example (T : TopologicalSpace X) (S : Setoid X) :
    QuotientTopologicalSpace T S = @instTopologicalSpaceQuotient X S T := rfl

-- Example:
--   X = â„, T = standard topology on â„
--   âˆ€ x, y âˆˆ X, x ~ y iff x - y âˆˆ â„¤
--   ----------------------------------------------
--   quotient space â„ / ~
--   projection map f : â„ â†’ â„ / ~ (f(x) = fraction part of x)
--   quotient (topological) space (â„ / ~, f_* T)

-- Define a relation on â„
def r_intdiff : â„ â†’ â„ â†’ Prop := fun x x' => âˆƒ z : â„¤, x - x' = z

def r_trivial : â„ â†’ â„ â†’ Prop := fun _ _ => True

def r_trivial2 : â„ â†’ â„ â†’ Prop := fun x y => x = y

-- Prove that the relation is an equivalence relation
instance eq_intdiff : Equivalence r_intdiff where
  refl x := by
    use 0
    rw [Int.cast_zero]
    exact sub_eq_zero_of_eq rfl
  symm := by
    intro x y
    rintro âŸ¨z, hâŸ©
    use -z
    rw [Int.cast_neg, â† h]
    exact (neg_sub x y).symm
  trans := by
    intro x y z
    rintro âŸ¨zâ‚, hâ‚âŸ© âŸ¨zâ‚‚, hâ‚‚âŸ©
    use zâ‚ + zâ‚‚
    rw [Int.cast_add, â† hâ‚, â† hâ‚‚]
    exact (sub_add_sub_cancel x y z).symm

instance eq_trivial : Equivalence r_trivial where
  refl := by
    intro _
    trivial
  symm := by
    intro x y _
    trivial
  trans := by
    intro x y z _ _
    trivial

instance eq_trivial2 : Equivalence r_trivial2 where
  refl := by
    intro _
    trivial
  symm := by
    intro x y h
    exact h.symm
  trans := by
    intro x y z hâ‚ hâ‚‚
    exact hâ‚.trans hâ‚‚

-- Define the bundle of equivalence relation
def setoid_â„_intdiff : Setoid â„ where
  iseqv := eq_intdiff

def setoid_â„_trivial : Setoid â„ where
  iseqv := eq_trivial

def setoid_â„_trivial2 : Setoid â„ where
  iseqv := eq_trivial2

-- Define the quotient space based on the equivalence relation
def quotient_â„_intdiff := Quotient setoid_â„_intdiff

def quotient_â„_trivial := Quotient setoid_â„_trivial

def quotient_â„_trivial2 := Quotient setoid_â„_trivial2

-- Define the projection map (not necessary)
def proj_â„_intdiff : â„ â†’ quotient_â„_intdiff := Quotient.mk setoid_â„_intdiff

def proj_â„_trivial : â„ â†’ quotient_â„_trivial := Quotient.mk setoid_â„_trivial

def proj_â„_trivial2 : â„ â†’ quotient_â„_trivial2 := Quotient.mk setoid_â„_trivial2

-- Prove that the equivalence relation defines
-- a topological space on the quotient space
instance T_quotient_â„_intdiff [T_â„ : TopologicalSpace â„]
  : TopologicalSpace quotient_â„_intdiff
  := QuotientTopologicalSpace T_â„ setoid_â„_intdiff

instance T_quotient_â„_trivial [T_â„ : TopologicalSpace â„]
  : TopologicalSpace quotient_â„_trivial
  := QuotientTopologicalSpace T_â„ setoid_â„_trivial

instance T_quotient_â„_trivial2 [T_â„ : TopologicalSpace â„]
  : TopologicalSpace quotient_â„_trivial2
  := QuotientTopologicalSpace T_â„ setoid_â„_trivial2

-- Product topology
-- Given a family of topological spaces {(Xáµ¢, Táµ¢) : i âˆˆ Î¹}
-- where the index set Î¹ is arbitrary, the product space âˆáµ¢ Xáµ¢
-- is a topological space with topology â¨…áµ¢ páµ¢^* Táµ¢
-- where páµ¢ : âˆáµ¢ Xáµ¢ â†’ Xáµ¢ is the projection map.
-- (infimum of the pullback topologies)
--
-- Motivation:
--   we want a topology such that
--   for any topological space (Z, T_Z) and
--       any function f : Z â†’ âˆáµ¢ Xáµ¢
--   f is continuous wrt (Z, T_Z) and (âˆáµ¢ Xáµ¢, â¨…áµ¢ páµ¢^* Táµ¢)
--   iff âˆ€ i âˆˆ Î¹, páµ¢ âˆ˜ f is continuous wrt (Z, T_Z) and (Xáµ¢, Táµ¢)
example (Î¹ : Type*) (X : Î¹ â†’ Type*) (T_X : âˆ€ i, TopologicalSpace (X i)) :
    (Pi.topologicalSpace : TopologicalSpace (âˆ€ i, X i)) =
      â¨… i, TopologicalSpace.induced (fun x â†¦ x i) (T_X i) :=
  rfl

-- Proof:
--   âˆ€ i âˆˆ Î¹, (páµ¢ âˆ˜ f) is continuous wrt (Z, T_Z) and (Xáµ¢, Táµ¢)
--   â‡” âˆ€ i âˆˆ Î¹, (páµ¢ âˆ˜ f)_* T_Z â‰¤ Táµ¢
--   â‡” âˆ€ i âˆˆ Î¹, páµ¢_* (f_* T_Z) â‰¤ Táµ¢
--   â‡” âˆ€ i âˆˆ Î¹, f_* T_Z â‰¤ páµ¢^* Táµ¢
--   â‡” f_* T_Z â‰¤ â¨…áµ¢ páµ¢^* Táµ¢
--   â‡” f is continuous wrt (Z, T_Z) and (âˆáµ¢ Xáµ¢, â¨…áµ¢ páµ¢^* Táµ¢)
example {Z : Type*} {Î¹ : Type*} (X : Î¹ â†’ Type*) (f : Z â†’ Î  i, X i) (T_Z : TopologicalSpace Z) (T_X : âˆ€ i, TopologicalSpace (X i))
  : Continuous[T_Z, Pi.topologicalSpace] f â†” âˆ€ i, Continuous[T_Z, T_X i] ((fun x â†¦ x i) âˆ˜ f) :=
  -- continuous_pi_iff
  by
  rw [continuous_iff_coinduced_le]
  rw [Pi.topologicalSpace]
  rw [le_iInf_iff]
  simp_rw [â† coinduced_le_iff_le_induced]
  simp_rw [coinduced_compose]
  simp_rw [â† continuous_iff_coinduced_le]

-- 10.3.2. Separation and countability

-- Separation axioms are properties of topological spaces
-- that impose constraints on the topology of the space
-- to ensure that points and sets can be separated from each other.

-- T2 (Hausdorff) space:
-- A topological space X is Hausdorff if for any two distinct points x, y âˆˆ X,
-- there exist disjoint open sets U, V âˆˆ X such that x âˆˆ U and y âˆˆ V.
example [TopologicalSpace X] [T2Space X] {x y : X} (hxy : x â‰  y) :
  âˆƒ U V : Set X, IsOpen U âˆ§ IsOpen V âˆ§ x âˆˆ U âˆ§ y âˆˆ V âˆ§ Disjoint U V :=
  t2_separation hxy

-- The indiscrete topology on a space X
def IndiscreteTopology {X : Type*} : TopologicalSpace X where
  IsOpen U := U = âˆ… âˆ¨ U = univ
  isOpen_univ := Or.inr rfl
  isOpen_inter U V := by
    rintro (rfl | rfl) (rfl | rfl)
    constructor
    Â· exact empty_inter âˆ…
    Â· exact Or.inl (empty_inter univ)
    constructor
    Â· exact inter_empty univ
    Â· exact Or.inr (univ_inter univ)
  isOpen_sUnion S := fun h => sUnion_mem_empty_univ h

-- The indiscrete topology on a space with at least two points is not Hausdorff.
example {X : Type*} (h' : âˆƒ x y : X, x â‰  y) :
    Â¬ @T2Space X IndiscreteTopology := by
  -- Let T be the indiscrete topology on X.
  -- Suppose T is Hausdorff.
  intro hT2
  -- Let x, y âˆˆ X be distinct points.
  rcases h' with âŸ¨x, y, hxyâŸ©
  -- Since T is Hausdorff, there exist disjoint open sets U, V âˆˆ X
  -- such that x âˆˆ U and y âˆˆ V.
  rcases hT2.t2 hxy with âŸ¨U, V, hU, hV, hxU, hyV, hUVâŸ©
  -- Since T is the indiscrete topology, U = âˆ… or U = univ
  -- and V = âˆ… or V = univ.
  -- If U = âˆ…, then x âˆˆ U is contradictory.
  -- If U = univ,
  --   if V = âˆ…, then y âˆˆ V is contradictory.
  --   if V = univ, then U and V being disjoint is contradictory.
  rcases hU with (rfl | rfl)
  Â· exact hxU
  Â· rcases hV with (rfl | rfl)
    Â· exact hyV
    Â· rw [univ_disjoint] at hUV
      rw [hUV] at hxU
      exact hxU

-- In a Hausdorff space, the limit of a sequence is unique.
example [TopologicalSpace X] [T2Space X] {u : â„• â†’ X} {a b : X} (ha : Tendsto u atTop (ğ“ a))
    (hb : Tendsto u atTop (ğ“ b)) : a = b :=
  tendsto_nhds_unique ha hb

-- In the topological space X with the indiscrete topology,
-- every sequence converges to every point.
-- (thus, the limit of a sequence is not unique)
example [T : TopologicalSpace X] {u : â„• â†’ X} {a : X} {h : T = IndiscreteTopology} :
  Tendsto u atTop (ğ“ a) := by
  rw [tendsto_nhds]
  intro S hS
  rw [h] at hS
  rcases hS with (rfl | rfl)
  Â· exact False.elim
  Â· exact fun _ => univ_mem

-- T3 (Regular) space:
-- A topological space X is regular if for any point x âˆˆ X and any closed set F âŠ† X
-- such that x âˆ‰ F, x and F admit disjoint neighborhoods.
example [TopologicalSpace X] [T3Space X] {x : X} {F : Set X} (hx : x âˆ‰ F) (hF : IsClosed F) :
  Disjoint (ğ“Ë¢ F) (ğ“ x) :=
  RegularSpace.regular hF hx

-- A regular space is Hausdorff.
example [TopologicalSpace X] [T3Space X] : T2Space X :=
  T3Space.t25Space.t2Space

-- Additionally, in a regular space, each point has a basis of closed neighborhoods.
example [TopologicalSpace X] [RegularSpace X] (a : X) :
    (ğ“ a).HasBasis (fun s : Set X â†¦ s âˆˆ ğ“ a âˆ§ IsClosed s) id :=
  closed_nhds_basis a

-- On the other hand, a topological space only has a basis of open neighborhoods.
example [TopologicalSpace X] {x : X} :
    (ğ“ x).HasBasis (fun t : Set X â†¦ t âˆˆ ğ“ x âˆ§ IsOpen t) id :=
  nhds_basis_opens' x

-- Example: K-topology
def KTopologicalSpace [StdTopo : TopologicalSpace X] (K : Set X) : TopologicalSpace X where
  IsOpen s :=
    -- An open set in the K-topology can be written in the form U \ B
    -- where U is open in the standard topology and B âŠ† K.
    âˆƒ U B, (IsOpen[StdTopo] U) âˆ§ (B âŠ† K) âˆ§ (s = U \ B)
  isOpen_univ := by
    -- Let U = â„ and B = âˆ….
    use univ, âˆ…
    -- We have:
    --   U = â„ is open in the standard topology,
    --   B = âˆ… âŠ† K, and
    --   â„ = â„ \ âˆ… = U \ B.
    -- Thus, â„ is open in the K-topology.
    exact âŸ¨StdTopo.isOpen_univ, empty_subset K, diff_empty.symmâŸ©
  isOpen_inter := by
    -- Suppose two sets Uâ‚› \ Bâ‚› and Uâ‚œ \ Bâ‚œ are open in the K-topology
    -- where Uâ‚›, Uâ‚œ be open sets in the standard topology
    -- and Bâ‚›, Bâ‚œ âŠ† K.
    rintro s t âŸ¨Us, Bs, hUs, hBsK, rflâŸ© âŸ¨Ut, Bt, hUt, hBtK, rflâŸ©
    -- Let U = Us âˆ© Ut and B = Bs âˆª Bt.
    use Us âˆ© Ut, Bs âˆª Bt
    constructor
    Â· -- Since a finite intersection of open sets is open,
      -- U = Uâ‚› âˆ© Uâ‚œ is open in the standard topology
      exact StdTopo.isOpen_inter Us Ut hUs hUt
    Â· constructor
      Â· -- Since Bâ‚›, Bâ‚œ âŠ† K, B = Bâ‚› âˆª Bâ‚œ âŠ† K.
        exact union_subset hBsK hBtK
      Â· -- (Uâ‚› \ Bâ‚›) âˆ© (Uâ‚œ \ Bâ‚œ) = (Uâ‚› âˆ© Bâ‚›á¶œ) âˆ© (Uâ‚œ âˆ© Bâ‚œá¶œ)
        --                       = (Uâ‚› âˆ© Uâ‚œ) âˆ© (Bâ‚›á¶œ âˆ© Bâ‚œá¶œ)
        --                       = (Uâ‚› âˆ© Uâ‚œ) âˆ© (Bâ‚› âˆª Bâ‚œ)á¶œ
        --                       = (Uâ‚› âˆ© Uâ‚œ) \ (Bâ‚› âˆª Bâ‚œ)
        rw [diff_eq, diff_eq, inter_inter_inter_comm, â† compl_union, â† diff_eq]
  isOpen_sUnion := by
    -- Let S be a collection of subsets of â„.
    -- Suppose each s âˆˆ S is of the form Uâ‚› \ Bâ‚›
    -- for some open set Uâ‚› and some subset Bâ‚› âŠ† K.
    intro S hS
    choose! U B hU hB hUB using hS
    -- Let U = â‹ƒ s âˆˆ S, Uâ‚› and B = K \ â‹ƒ S.
    use (â‹ƒ s âˆˆ S, U s), K \ (â‹ƒâ‚€ S)
    -- We need to show 3 things:
    --   1. U is open in the standard topology.
    --   2. B âŠ† K.
    --   3. â‹ƒ S = U \ B.
    constructor
    Â· -- 1. Show: U is open in the standard topology.
      -- Since each Uâ‚› is open in the standard topology,
      -- U = â‹ƒ s âˆˆ S, Uâ‚› is open in the standard topology.
      rw [â† sUnion_image]
      apply StdTopo.isOpen_sUnion
      rintro V âŸ¨U', hU'S, rflâŸ©
      exact hU U' hU'S
    Â· constructor
      Â· -- 2. Show: B âŠ† K.
        -- B = K \ â‹ƒ S, so B âŠ† K.
        exact diff_subset
      Â· -- 3. Show: â‹ƒ S = U \ B.
        -- U \ B = (â‹ƒ s âˆˆ S, Uâ‚›) \ (K \ â‹ƒ S)
        --       = (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© (K \ â‹ƒ S)á¶œ
        --       = (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© (K âˆ© (â‹ƒ S)á¶œ)á¶œ
        --       = (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© (Ká¶œ âˆª (â‹ƒ S)á¶œá¶œ)
        --       = (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© (Ká¶œ âˆª â‹ƒ S)
        --       = (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© Ká¶œ âˆª (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© â‹ƒ S
        --       = (â‹ƒ s âˆˆ S, Uâ‚›) \ K âˆª (â‹ƒ s âˆˆ S, Uâ‚›) âˆ© â‹ƒ S
        rw [diff_eq, diff_eq, compl_inter, compl_compl, inter_union_distrib_left, â† diff_eq]

        -- Show: â‹ƒ S âŠ† â‹ƒ s âˆˆ S, Uâ‚›
        have hâ‚ : â‹ƒâ‚€ S âŠ† â‹ƒ s âˆˆ S, U s := by
          -- Let x âˆˆ â‹ƒ S. Then, âˆƒ s âˆˆ S, x âˆˆ Uâ‚› \ Bâ‚›.
          rintro x âŸ¨s, hsS, hxsâŸ©
          rw [hUB s hsS] at hxs
          -- Then, âˆƒ s âˆˆ S, x âˆˆ Uâ‚›. Thus, x âˆˆ â‹ƒ s âˆˆ S, Uâ‚›.
          rw [â† sUnion_image]
          use U s, mem_image_of_mem U hsS, mem_of_mem_diff hxs
        -- U \ B = (â‹ƒ s âˆˆ S, Uâ‚›) \ K âˆª â‹ƒ S
        rw [inter_eq_self_of_subset_right hâ‚]

        -- Show: (â‹ƒ s âˆˆ S, Uâ‚›) \ K âŠ† â‹ƒ S
        have hâ‚‚ : (â‹ƒ s âˆˆ S, U s) \ K âŠ† â‹ƒâ‚€ S := by
          -- Let x âˆˆ (â‹ƒ s âˆˆ S, Uâ‚›) \ K. Then, x âˆˆ â‹ƒ s âˆˆ S, Uâ‚› and x âˆ‰ K.
          -- Thus, âˆƒ s âˆˆ S, x âˆˆ Uâ‚›. Consider this s.
          intro x hx
          rw [â† sUnion_image] at hx
          rcases hx with âŸ¨âŸ¨_, âŸ¨s, hsS, rflâŸ©, hxUsâŸ©, hnxKâŸ©
          -- We can show that x âˆ‰ Bâ‚› since x âˆ‰ K and Bâ‚› âŠ† K.
          have hxnBs : x âˆ‰ B s := fun hxBs â†¦ hnxK (hB s hsS hxBs)
          -- Thus, x âˆˆ Uâ‚› \ Bâ‚›.
          -- In other words, âˆƒ s âˆˆ S, x âˆˆ Uâ‚› \ Bâ‚› âˆˆ S.
          -- Therefore, x âˆˆ â‹ƒ S.
          use s, hsS
          rw [hUB s hsS]
          exact mem_diff_of_mem hxUs hxnBs
        -- U \ B = â‹ƒ S
        rw [union_eq_self_of_subset_left hâ‚‚]

-- The K-topology on â„ is Hausdorff.
example [StdTopo: TopologicalSpace X] [StdT2: T2Space X] (K : Set X)
  : @T2Space X (KTopologicalSpace K) := by
  -- A topological space is Hausdorff if for any two distinct points x, y âˆˆ X,
  -- there exist disjoint open sets U, V âˆˆ X such that x âˆˆ U and y âˆˆ V.
  rw [t2Space_iff]
  -- Let x, y âˆˆ X be distinct points.
  -- Show: there exist disjoint sets U, V âˆˆ X such that
  -- U, V are open with respect to the K-topology and
  -- x âˆˆ U and y âˆˆ V.
  intro x y hxy
  -- Since X with the standard topology is Hausdorff,
  -- there exist disjoint sets U, V âˆˆ X such that
  -- U, V are open with respect to the standard topology and
  -- x âˆˆ U and y âˆˆ V.
  rcases StdT2.t2 hxy with âŸ¨U, V, hU, hV, hxU, hyV, hUVâŸ©
  -- Since U, V are open with respect to the standard topology,
  -- U, V are open with respect to the K-topology.
  have hK {S : Set X} (h : IsOpen[StdTopo] S) : IsOpen[KTopologicalSpace K] S :=
    âŸ¨S, âˆ…, h, empty_subset K, diff_empty.symmâŸ©
  -- Thus, U, V are the sets we are looking for.
  use U, V, hK hU, hK hV, hxU, hyV, hUV

-- Define K = {1 / (n + 1) : n âˆˆ â„•}.
def K : Set â„ := Set.range (fun (n : â„•) => 1 / (n + 1))

-- Show that there is no irrational number in K.
lemma Irrat_notin_K : âˆ€ x : â„, Irrational x â†’ x âˆ‰ K := by
  -- Let x âˆˆ â„ be an irrational number.
  intro x hx
  -- Suppose x âˆˆ K.
  by_contra! hxK
  -- Then, x can be written as 1 / (n + 1) for some natural number n.
  rcases Set.mem_range.mp hxK with âŸ¨n, rflâŸ©
  -- Then, 1 / (n + 1) is irrational. This is a contradiction.
  rw [Irrational] at hx
  apply hx
  use 1 / (n + 1)
  rw [Rat.cast_div, Rat.cast_one, Rat.cast_add, Rat.cast_one, Rat.cast_natCast]

example : Â¬ @RegularSpace â„ (KTopologicalSpace K) := by
  -- We prove by contradiction.
  -- Suppose the K-topology on â„ is regular.
  by_contra! h
  -- Then, for all closed set F in the K-topology and all x âˆ‰ F,
  -- x and F admit disjoint neighborhoods.
  rw [regularSpace_iff] at h

  -- We show that K is closed in the K-topology.
  have hK : IsClosed[KTopologicalSpace K] K := by
    -- Note that Ká¶œ = â„ \ K =: U \ B.
    -- We have
    --   U = â„ is open in the standard topology,
    --   K âŠ† K, and
    --   Ká¶œ = â„ \ K.
    -- Thus, Ká¶œ is open in the K-topology,
    -- which implies K is closed in the K-topology.
    use univ, K
    exact âŸ¨isOpen_univ, Subset.refl K, compl_eq_univ_diff KâŸ©

  -- We show that 0 is not in K.
  have h0nK : 0 âˆ‰ K := by
    -- Suppose 0 âˆˆ K.
    by_contra! h0K
    -- Then, 0 can be written as 1 / (n + 1) for some natural number n.
    rcases Set.mem_range.mp h0K with âŸ¨n, hnâŸ©
    -- Since 1 / (n + 1) = 0, either 1 = 0 or n + 1 = 0.
    rcases (div_eq_zero_iff.mp hn) with (h' | h')
    Â· -- 1 = 0 is contradictory.
      exact one_ne_zero h'
    Â· -- n + 1 is the successor of a natural number.
      -- Thus, n + 1 â‰  0. So n + 1 = 0 is contradictory.
      rw [â† Nat.cast_succ, Nat.cast_eq_zero] at h'
      exact Nat.succ_ne_zero n h'

  -- Since K is closed in the K-topology and 0 âˆ‰ K,
  -- 0 and K admit disjoint neighborhoods.
  -- Then, there exist disjoint sets U, V
  -- such that K is in the neighborhood of U
  -- and 0 is in the neighborhood of V.
  rcases Filter.disjoint_iff.mp (h hK h0nK) with âŸ¨U, hU, âŸ¨V, hV, hUVâŸ©âŸ©

  -- We show that if a set U is in the neighborhood of a point x,
  -- then there exists a radius Îµ > 0 such that the open interval (x - Îµ, x + Îµ)
  -- excluding points of K, i.e. (x - Îµ, x + Îµ) \ K, is a subset of U.
  have aux {x : â„} {U : Set â„} (hUx : U âˆˆ @nhds â„ (KTopologicalSpace K) x) :
    âˆƒ Îµ > 0, Ioo (x - Îµ) (x + Îµ) \ K âŠ† U := by
    -- Let U be in the neighborhood of x.
    -- Then, there exists an open set U' âŠ† U in the K-topology such that x âˆˆ U'.
    rw [@mem_nhds_iff â„ x U (KTopologicalSpace K)] at hUx
    rcases hUx with âŸ¨U', hU'U, hU', hxU'âŸ©
    -- Since U' is open in the K-topology,
    -- there exists an open set U'' in the standard topology
    -- and a subset B'' âŠ† K such that U' = U'' \ B''.
    rw [isOpen_mk] at hU'
    rcases hU' with âŸ¨U'', B'', hU'', hB''K, rflâŸ©
    -- We show that there exists Îµ > 0 such that (x - Îµ, x + Îµ) âŠ† U''.
    have : âˆƒ Îµ > 0, Ioo (x - Îµ) (x + Îµ) âŠ† U'' := by
      -- Since x âˆˆ U' = U'' \ B'', x âˆˆ U''.
      -- Since U'' is open in the standard topology and x âˆˆ U'',
      -- U'' is in the neighborhood of x.
      have : U'' âˆˆ ğ“ x := (IsOpen.mem_nhds_iff hU'').mpr (mem_of_mem_diff hxU')
      -- On â„, this implies there exists l < u
      -- such that x âˆˆ (l, u) âŠ† U''.
      rw [mem_nhds_iff_exists_Ioo_subset] at this
      rcases this with âŸ¨l, u, âŸ¨hl, huâŸ©, hIluU'âŸ©
      -- Let Îµ = min {x - l, u - x}. Then Îµ â‰¤ x - l and Îµ â‰¤ u - x.
      use min (x - l) (u - x)
      constructor
      Â· -- Since l < x, x - l > 0. Similarly, u - x > 0. Thus, Îµ > 0.
        exact lt_min (sub_pos.mpr hl) (sub_pos.mpr hu)
      Â· -- Let y âˆˆ (x - Îµ, x + Îµ).
        rintro y âŸ¨hyleft, hyrightâŸ©
        -- Then, l = x - (x - l) â‰¤ x - Îµ < y.
        have hly := calc
          l = x - (x - l) := (sub_sub_self x l).symm
          _ â‰¤ x - min (x - l) (u - x) := sub_le_sub_left (min_le_left (x - l) (u - x)) x
          _ < y := hyleft
        -- On the other hand, y < x + Îµ â‰¤ x + (u - x) = u.
        have hyu := calc
          y < x + min (x - l) (u - x) := hyright
          _ â‰¤ x + (u - x) := add_le_add_left (min_le_right (x - l) (u - x)) x
          _ = u := add_sub_cancel x u
        -- Thus, y âˆˆ (l, u) âŠ† U''.
        exact hIluU' âŸ¨hly, hyuâŸ©
    rcases this with âŸ¨Îµ, hÎµ, hIU''âŸ©
    -- Use this Îµ as the radius.
    use Îµ, hÎµ
    -- Let y âˆˆ (x - Îµ, x + Îµ) \ K. Then, y âˆˆ (x - Îµ, x + Îµ) and y âˆ‰ K.
    rintro y âŸ¨hyI, hynKâŸ©
    -- Since y âˆˆ (x - Îµ, x + Îµ), y âˆˆ U''.
    -- Since y âˆ‰ K âŠ‡ B'', y âˆ‰ B''.
    -- Thus, y âˆˆ U'' \ B'' = U' âŠ† U.
    exact hU'U (mem_diff_of_mem (hIU'' hyI) (fun hyB'' â†¦ hynK (hB''K hyB'')))

  -- Apply the auxiliary lemma to V, which is in the neighborhood of 0.
  -- Then, there exists Îµ > 0 such that
  -- (-Îµ, Îµ) \ K âŠ† V.
  rcases aux hV with âŸ¨Îµ, hÎµ, hIdKVâŸ©
  rw [zero_sub, zero_add] at hIdKV
  -- Since Îµ > 0, there exists a natural number n such that 1 / (n + 1) < Îµ.
  rcases exists_nat_one_div_lt hÎµ with âŸ¨n, hnâŸ©
  -- Let x = 1 / (n + 1).
  let x := 1 / ((n : â„) + 1)
  -- Then, x âˆˆ K.
  have hxK : x âˆˆ K := Set.mem_range.mpr âŸ¨n, rflâŸ©

  -- Since U is in the neighborhood of K,
  -- there exists an open set U' in the K-topology such that
  -- K âŠ† U' âŠ† U.
  rw [@mem_nhdsSet_iff_exists â„ (KTopologicalSpace K) U K] at hU
  rcases hU with âŸ¨U', hU', hKU', hU'UâŸ©
  -- Since U' is open in the K-topology,
  -- for every point y âˆˆ U', U' is in the neighborhood of y.
  rw [@isOpen_iff_mem_nhds] at hU'
  -- Since x âˆˆ K âŠ† U' âŠ† U, x âˆˆ U. Thus, U' is in the neighborhood of x.
  -- Apply the auxiliary lemma to U', there exists Îµ' > 0 such that
  -- (x - Îµ', x + Îµ') \ K âŠ† U'.
  rcases aux (hU' x (hKU' hxK)) with âŸ¨Îµ', hÎµ', hIdKU'âŸ©

  -- If (x - Îµ', x + Îµ') and (-Îµ, Îµ) intersect
  -- at a point t that is not in K, i.e.,
  -- âˆƒ t âˆˆ (x - Îµ', x + Îµ') âˆ© (-Îµ, Îµ) \ K, then
  -- t âˆˆ (x - Îµ', x + Îµ') \ K âŠ† U' âŠ† U and
  -- t âˆˆ (-Îµ, Îµ) \ K âŠ† V and so
  -- U and V are not disjoint, which is a contradiction.
  have aux2 {t : â„} (htnK : t âˆ‰ K) :
    Â¬ (t âˆˆ Ioo (x - Îµ') (x + Îµ') âˆ§ t âˆˆ Ioo (-Îµ) Îµ) := by
    push_neg
    intro htUK htVK
    rw [disjoint_left] at hUV
    apply hUV
      (hU'U (hIdKU' (mem_diff_of_mem htUK htnK)))
      (hIdKV (mem_diff_of_mem htVK htnK))

  -- We show that such a point t exists.
  -- Consider two cases: x - Îµ' â‰¤ -Îµ and x - Îµ' > -Îµ.
  by_cases hÎµÎµ' : x - Îµ' â‰¤ -Îµ
  Â· -- Case 1: x - Îµ' â‰¤ -Îµ.
    -- We know that 0 âˆ‰ K.
    -- Since x - Îµ' â‰¤ -Îµ < 0, x - Îµ' < 0.
    -- Since x = 1 / (n + 1) > 0, x + Îµ' > 0.
    -- Thus, 0 âˆˆ (x - Îµ', x + Îµ').
    -- On the other hand, it is obvious that 0 âˆˆ (-Îµ, Îµ).
    -- Thus, 0 âˆˆ (x - Îµ', x + Îµ') âˆ© (-Îµ, Îµ) and 0 âˆ‰ K.
    exact aux2 h0nK âŸ¨
      âŸ¨
        lt_of_le_of_lt hÎµÎµ' (neg_neg_iff_pos.mpr hÎµ),
        gt_trans (lt_add_of_pos_right x hÎµ') (Nat.one_div_pos_of_nat)
      âŸ©,
      âŸ¨neg_neg_iff_pos.mpr hÎµ, hÎµâŸ©
    âŸ©
  Â· -- Cases 2: x - Îµ' > -Îµ.
    push_neg at hÎµÎµ'
    -- Since x - Îµ' < x, there exists an irrational number r âˆˆ (x - Îµ', x).
    rcases exists_irrational_btwn (sub_lt_self x hÎµ') with âŸ¨r, hr, h1r, hr1âŸ©
    -- An irrational number cannot be in the form 1 / (n + 1). Thus, r âˆ‰ K.
    -- Since r âˆˆ (x - Îµ', x) âŠ† (x - Îµ', x + Îµ'), r âˆˆ (x - Îµ', x + Îµ').
    -- Since r > x - Îµ' > -Îµ, r > -Îµ. On the other hand, r < x < Îµ. Thus, r âˆˆ (-Îµ, Îµ).
    -- Therefore, r âˆˆ (x - Îµ', x + Îµ') âˆ© (-Îµ, Îµ) and r âˆ‰ K.
    exact aux2 (Irrat_notin_K r hr) âŸ¨
      âŸ¨h1r, lt_add_of_lt_of_pos hr1 hÎµ'âŸ©,
      âŸ¨gt_trans h1r hÎµÎµ', gt_trans hn hr1âŸ©
    âŸ©

theorem aux {X Y A : Type*} [TopologicalSpace X] {c : A â†’ X}
      {f : A â†’ Y} {x : X} {F : Filter Y}
      (h : Tendsto f (comap c (ğ“ x)) F) {V' : Set Y} (V'_in : V' âˆˆ F) :
    âˆƒ V âˆˆ ğ“ x, IsOpen V âˆ§ c â»Â¹' V âŠ† f â»Â¹' V' := by
  rcases h V'_in with âŸ¨U, hUinNx, hcinvUssfinvV'âŸ©
  rw [mem_nhds_iff] at hUinNx
  rcases hUinNx with âŸ¨V, hVssU, hopenV, hxinVâŸ©
  use V
  exact âŸ¨
    IsOpen.mem_nhds hopenV hxinV,
    hopenV,
    fun _ hxincinvV => hcinvUssfinvV' (hVssU hxincinvV),
  âŸ©

example [TopologicalSpace X] [TopologicalSpace Y] [T3Space Y] {A : Set X}
    (hA : âˆ€ x, x âˆˆ closure A) {f : A â†’ Y} (f_cont : Continuous f)
    (hf : âˆ€ x : X, âˆƒ c : Y, Tendsto f (comap (â†‘) (ğ“ x)) (ğ“ c)) :
    âˆƒ Ï† : X â†’ Y, Continuous Ï† âˆ§ âˆ€ a : A, Ï† a = f a := by
  choose Ï† hÏ† using hf
  use Ï†
  constructor
  Â· rw [continuous_iff_continuousAt]
    intro x
    suffices âˆ€ V' âˆˆ ğ“ (Ï† x), IsClosed V' â†’ Ï† â»Â¹' V' âˆˆ ğ“ x by
      rw [continuousAt_def]
      intro A hAinNÏ†x
      rcases (closed_nhds_basis (Ï† x)).mem_iff.mp hAinNÏ†x with âŸ¨V', âŸ¨hV'inNÏ†x, hclosedV'âŸ©, hV'ssAâŸ©
      exact mem_of_superset (this V' hV'inNÏ†x hclosedV') (preimage_mono hV'ssA)
    intro V' hV'inNÏ†x hclosedV'
    rcases aux (hÏ† x) hV'inNÏ†x with âŸ¨V, hVinNx, hopenV, hcinvVssfinvV'âŸ©
    apply mem_of_superset hVinNx
    intro y hyV
    have hVinNy: V âˆˆ ğ“ y := hopenV.mem_nhds hyV
    have := mem_closure_iff_comap_neBot.mp (hA y)
    apply hclosedV'.mem_of_tendsto (hÏ† y)
    exact mem_of_superset (preimage_mem_comap hVinNy) hcinvVssfinvV'
  Â· intro a
    apply tendsto_nhds_unique _ f_cont.continuousAt
    rw [nhds_induced]
    exact hÏ† a

#check HasBasis.tendsto_right_iff

example [TopologicalSpace X] [FirstCountableTopology X]
      {s : Set X} {a : X} :
    a âˆˆ closure s â†” âˆƒ u : â„• â†’ X, (âˆ€ n, u n âˆˆ s) âˆ§ Tendsto u atTop (ğ“ a) :=
  mem_closure_iff_seq_limit

variable [TopologicalSpace X]

example {F : Filter X} {x : X} : ClusterPt x F â†” NeBot (ğ“ x âŠ“ F) :=
  Iff.rfl

example {s : Set X} :
    IsCompact s â†” âˆ€ (F : Filter X) [NeBot F], F â‰¤ ğ“Ÿ s â†’ âˆƒ a âˆˆ s, ClusterPt a F :=
  Iff.rfl

example [FirstCountableTopology X] {s : Set X} {u : â„• â†’ X} (hs : IsCompact s)
    (hu : âˆ€ n, u n âˆˆ s) : âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Tendsto (u âˆ˜ Ï†) atTop (ğ“ a) :=
  hs.tendsto_subseq hu

variable [TopologicalSpace Y]

example {x : X} {F : Filter X} {G : Filter Y} (H : ClusterPt x F) {f : X â†’ Y}
    (hfx : ContinuousAt f x) (hf : Tendsto f F G) : ClusterPt (f x) G :=
  ClusterPt.map H hfx hf

example [TopologicalSpace Y] {f : X â†’ Y} (hf : Continuous f) {s : Set X} (hs : IsCompact s) :
    IsCompact (f '' s) := by
  intro F F_ne F_le
  have map_eq : map f (ğ“Ÿ s âŠ“ comap f F) = ğ“Ÿ (f '' s) âŠ“ F := by
    rw [Filter.push_pull]
    rw [map_principal]
  have Hne : (ğ“Ÿ s âŠ“ comap f F).NeBot := by
    apply NeBot.of_map
    rw [map_eq]
    rw [inf_of_le_right F_le]
    exact F_ne
  have Hle : ğ“Ÿ s âŠ“ comap f F â‰¤ ğ“Ÿ s := inf_le_left
  rcases hs Hle with âŸ¨x, hxs, hclsptxâŸ©
  use f x
  constructor
  Â· exact mem_image_of_mem f hxs
  Â· apply hclsptx.map hf.continuousAt
    rw [Tendsto, map_eq]
    exact inf_le_right

example {Î¹ : Type*} {s : Set X} (hs : IsCompact s) (U : Î¹ â†’ Set X) (hUo : âˆ€ i, IsOpen (U i))
    (hsU : s âŠ† â‹ƒ i, U i) : âˆƒ t : Finset Î¹, s âŠ† â‹ƒ i âˆˆ t, U i :=
  hs.elim_finite_subcover U hUo hsU
