import MIL.Common
import Mathlib.Topology.Instances.Real
import Mathlib.Analysis.Normed.Operator.BanachSteinhaus

open Set Filter
open Topology Filter

-- 10.2. Metric spaces

-- Let X be a metric space.
-- Let a, b, c be points in X.
variable {X : Type*} [MetricSpace X] (a b c : X)

-- A metric space is a set of points equipped with a metric.
-- The metric is way to measure the distance between two points.

-- `dist` is a function XÂ² â†’ â„.
-- Input: two points a and b in X
-- Output: the distance in â„ between a and b
#check (dist a b : â„)

-- A metric is a distance function satisfies the following properties:
-- 1. Non-negativity: the distance between two points is always non-negative.
--      dist(a, b) â‰¥ 0, âˆ€ a, b âˆˆ X
#check (dist_nonneg : 0 â‰¤ dist a b)
-- 2. The distance between two points is zero iff the points are the same.
--     dist(a, b) = 0 â†” a = b, âˆ€ a, b âˆˆ X
#check (dist_eq_zero : dist a b = 0 â†” a = b)
-- 3. Symmetry: the distance between two points is the same in both directions.
--     dist(a, b) = dist(b, a), âˆ€ a, b âˆˆ X
#check (dist_comm a b : dist a b = dist b a)
-- 4. The triangle inequality: the distance between two points is always â‰¤ to the sum of
--                             the distances between the points and a third point.
--     dist(a, c) â‰¤ dist(a, b) + dist(b, c), âˆ€ a, b, c âˆˆ X
#check (dist_triangle a b c : dist a c â‰¤ dist a b + dist b c)

-- Example:
-- 1. Real numbers â„ is a metric space with
--      dist(a, b) = |a - b| where |.| is the absolute value function.
-- 2. Euclidean space â„â¿ is a metric space with
--      dist(a, b) = âˆš(âˆ‘áµ¢ (aáµ¢ - báµ¢)Â²) where a, b âˆˆ â„â¿

-- `EMetricSpace`: extended metric space
--   allows the distance to be infinite
#check EMetricSpace
-- `PseudoMetricSpace`: pseudo metric space
--   allows the distance between two distinct points to be zero
#check PseudoMetricSpace
-- `PseudoEMetricSpace`: pseudo extended metric space
--   (combining `EMetricSpace` and `PseudoMetricSpace`)
--   allows the distance between two points to be infinite
--   and the distance between two distinct points to be zero
#check PseudoEMetricSpace

-- 10.2.1. convergence and continuity in metric spaces

-- Metric spaces allow defining
--   convergence of sequences
--   continuity of functions between metric spaces
-- using the distance function.

-- Classic epsilon-N definition of convergence:
-- A sequence {uâ‚™} in a metric space converges to a point a iff
-- for every Îµ > 0, there exists an N such that
-- for all n â‰¥ N, dist(uâ‚™, a) < Îµ.
example {u : â„• â†’ X} {a : X} :
    Tendsto u atTop (ğ“ a) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, dist (u n) a < Îµ :=
  Metric.tendsto_atTop

-- Classic epsilon-delta definition of continuity:
-- A function f between two metric spaces is continuous iff
-- for all x in X, for all Îµ > 0, there exists a Î´ > 0 such that
-- for all x' in X, if dist(x', x) < Î´, then dist(f(x'), f(x)) < Îµ.
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} :
    Continuous f â†”
      âˆ€ x : X, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x', dist x' x < Î´ â†’ dist (f x') (f x) < Îµ :=
  Metric.continuous_iff

-- The function that measures the distance between two points is continuous.
-- If X and Y are metric spaces, the Cartesian product X Ã— Y is a metric space.
-- So, XÂ² is a metric space.
-- â„ is a metric space.
-- The distance function goes between two metric spaces, so we can talk about continuity.
-- The distance function is continuous.
example {X : Type*} [MetricSpace X] : Continuous fun p : X Ã— X â†¦ dist p.1 p.2 :=
  by continuity

-- Moreover, the function that measures the distance between
-- the images of points under a continuous function is also continuous.
-- i.e., if f is a continuous function from X to Y
-- then the function p â†¦ dist (f p.1) (f p.2) is also continuous.

-- Proof with the `continuity` tactic
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) := by continuity

-- Proof with a proof term
-- `continuous_fst` and `continuous_snd`: projections are continuous
example {X : Type*} [MetricSpace X] : Continuous (Prod.fst : X Ã— X â†’ X) := continuous_fst
example {X : Type*} [MetricSpace X] : Continuous (Prod.snd : X Ã— X â†’ X) := continuous_snd
-- `Continuous.comp`: composition of continuous functions is continuous
example {X Y Z : Type*} [MetricSpace X] [MetricSpace Y] [MetricSpace Z]
        {f : X â†’ Y} {g : Y â†’ Z} (hf : Continuous f) (hg : Continuous g) :
    Continuous (g âˆ˜ f) := hg.comp hf
-- `Continuous.prod_mk`: pairing of continuous functions is continuous
--   If f, g are continuous then (f Ã— g)(x) := (f(x), g(x)) is continuous.
example {X Y Z : Type*} [MetricSpace X] [MetricSpace Y] [MetricSpace Z]
        {f : X â†’ Y} {g : X â†’ Z} (hf : Continuous f) (hg : Continuous g) :
    Continuous (fun x : X â†¦ (f x, g x)) := Continuous.prod_mk hf hg
-- `Î» p â†¦ dist (f p.1) (f p.2)` = dist âˆ˜ ((f âˆ˜ `Prod.fst`) Ã— (f âˆ˜ `Prod.snd`))
-- `apply Continuous.comp` will not recognize the definitionally equality
-- However, a full proof term works
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) :=
  continuous_dist.comp ((hf.comp continuous_fst).prod_mk (hf.comp continuous_snd))

-- Proof with `Continuous.dist`:
--   If f, g are continuous functions, then x â†¦ dist(f(x), g(x)) is continuous.
--   `Continuous f â†’ Continuous g â†’ Continuous (fun x â†¦ dist (f x) (g x))`
-- Tactic mode:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) := by
  apply Continuous.dist
  exact hf.comp continuous_fst
  exact hf.comp continuous_snd
-- Term mode:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) :=
  (hf.comp continuous_fst).dist (hf.comp continuous_snd)

-- Proof with `Continuous.prodMap`:
-- `Continuous.prodMap`: If f, g are continuous functions,
--                       then (f Ã— g)(pâ‚, pâ‚‚) := (f(pâ‚), g(pâ‚‚)) is continuous.
example {X Y Z : Type*} [MetricSpace X] [MetricSpace Y] [MetricSpace Z]
        {f : X â†’ Y} {g : X â†’ Z} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun p : X Ã— X â†¦ (f p.1, g p.2) := hf.prodMap hg
-- `Î» p â†¦ dist (f p.1) (f p.2)` = dist âˆ˜ (f Ã— f)
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) := continuous_dist.comp (hf.prodMap hf)

-- Proof with `Continuous.fst'` and `Continuous.snd'`:
-- `Continuous.fst'`: image of projection to the first coordinate is continuous
--   If f is continuous, then (f âˆ˜ `Prod.fst`) is continuous.
-- Similarly, `Continuous.snd'`: If f is continuous, then (f âˆ˜ `Prod.snd`) is continuous.
-- Problem: obfuscates the proof
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} (hf : Continuous f) :
    Continuous fun p : X Ã— X â†¦ dist (f p.1) (f p.2) :=
  hf.fst'.dist hf.snd'

-- If f is continuous, then f(xÂ² + x) is continuous.
-- `continuous_pow`: x â†¦ xâ¿, denoted â¬â¿,  is continuous for all n âˆˆ â„•
#check continuous_pow
-- `continuous_id`: x â†¦ x, denoted id, is continuous
#check continuous_id
-- `Î» x â†¦ f (x ^ 2 + x)` = f âˆ˜ (â¬Â² + id)
example {f : â„ â†’ X} (hf : Continuous f) : Continuous fun x : â„ â†¦ f (x ^ 2 + x) :=
  hf.comp ((continuous_pow 2).add continuous_id)

-- A function f is continuous at a point a iff for every Îµ > 0,
-- there exists a Î´ > 0 s.t. for all x, if dist(x, a) < Î´, then dist(f(x), f(a)) < Îµ.
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] (f : X â†’ Y) (a : X) :
    ContinuousAt f a â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {x}, dist x a < Î´ â†’ dist (f x) (f a) < Îµ :=
  Metric.continuousAt_iff

-- A function is continuous iff it is continuous at every point.
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] (f : X â†’ Y) :
    Continuous f â†” âˆ€ a, ContinuousAt f a := continuous_iff_continuousAt

-- 10.2.2. Balls, open sets and closed sets

-- Let r be a real number.
variable (r : â„)

-- `Metric.ball a r`: the open ball centered at a with radius r
--   set of all points whose distance from a is strictly less than r
-- Example: an open ball in â„ is an open interval (a - r, a + r)
-- Example: an open ball in â„Â² is a disk without its boundary circle
example : Metric.ball a r = { b | dist b a < r } :=
  rfl

-- `Metric.closedBall a r`: the closed ball centered at a with radius r
--   set of all points whose distance from a is less than or equal to r
-- Example: a closed ball in â„ is a closed interval [a - r, a + r]
-- Example: a closed ball in â„Â² is a disk with its boundary circle
example : Metric.closedBall a r = { b | dist b a â‰¤ r } :=
  rfl

-- There is no sign restriction on the radius r.
-- If r is negative, any open or closed ball is empty.
-- If r is zero, any open ball is empty, and the closed ball is a singleton.

-- If r is positive, the center a is in the open ball.
example (hr : 0 < r) : a âˆˆ Metric.ball a r :=
  Metric.mem_ball_self hr

-- If r is non-negative, the center a is in the closed ball.
example (hr : 0 â‰¤ r) : a âˆˆ Metric.closedBall a r :=
  Metric.mem_closedBall_self hr

-- A set is open iff for every point in the set,
-- there exists an open ball centered at the point
-- that is entirely contained in the set.
-- Intuition: an open set doesn't include its boundary.
-- Intuition: every point has a wiggle room to move around without leaving the set.
-- Formally, a set s is open iff âˆ€ x âˆˆ s, âˆƒ Îµ > 0, B(x, Îµ) âŠ† s.
example (s : Set X) : IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, Metric.ball x Îµ âŠ† s :=
  Metric.isOpen_iff

-- Example: an open ball is an open set
example (a : X) (r : â„) : IsOpen (Metric.ball a r) :=
  -- Simple Lean proof:
  -- Metric.isOpen_ball
  -- Long proof:
  by
  rw [Metric.isOpen_iff]
  -- Consider x âˆˆ B(a, r).
  -- Show: there exists Îµ > 0 s.t. B(x, Îµ) âŠ† B(a, r).
  intro x hx
  -- Let Îµ = r - dist(x, a).
  use r - dist x a
  constructor
  Â· -- Show Îµ > 0.
    -- As x âˆˆ B(a, r), dist(x, a) < r. Thus, Îµ = r - dist(x, a) > 0.
    exact sub_pos.mpr hx
  Â· -- Let y âˆˆ B(x, Îµ).
    -- Show y âˆˆ B(a, r).
    intro y hy
    -- As y âˆˆ B(x, Îµ), dist(y, x) < Îµ = r - dist(x, a).
    -- Show dist(y, a) < r.
    rw [Metric.mem_ball] at *
    -- dist(y, a) â‰¤ dist(y, x) + dist(x, a) (âˆµ triangle inequality)
    --            < r - dist(x, a) + dist(x, a) (âˆµ dist(y, x) < r - dist(x, a))
    --            = r
    calc
      dist y a â‰¤ dist y x + dist x a := dist_triangle y x a
      _ < r - dist x a + dist x a := add_lt_add_right hy (dist x a)
      _ = r := sub_add_cancel r (dist x a)

-- A set is closed iff its complement is open.
-- Intuition: a closed set includes its boundary.
-- Intuition: a closed set contains all its limit points.
example {s : Set X} : IsClosed s â†” IsOpen (sá¶œ) :=
  isOpen_compl_iff.symm

-- Example: a closed ball is a closed set
example (a : X) (r : â„) : IsClosed (Metric.closedBall a r) := Metric.isClosed_ball

-- A closed set is closed under limits.

-- `mem_of_tendsto`: general version
--   if a function is *eventually* in a closed set
--   and converges to a point along a non-trivial filter,
--   then the limit point is in the closed set.

-- If a sequence is in a closed set and converges to a limit point,
-- then the limit point is also in the closed set.
-- Given
--   a set s
--   a point a
--   a sequence (uâ‚™)
-- If
--   s is closed
--   (uâ‚™) converges to a
--   âˆ€ n âˆˆ â„•, uâ‚™ âˆˆ s
-- Then
--   a âˆˆ s
example {s : Set X} {a : X} {u : â„• â†’ X}
  (hs : IsClosed s) (hu : Tendsto u atTop (ğ“ a)) (hus : âˆ€ n, u n âˆˆ s)
  : a âˆˆ s :=
  -- `mem_of_tendsto` only needs eventually uâ‚™ âˆˆ s, not all uâ‚™ âˆˆ s
  hs.mem_of_tendsto hu (Eventually.of_forall hus)

-- The closure of a set is the smallest closed set that contains the set.
-- Intuition: the closure "fills in the gaps" in a set.
-- Notation: cl(s)

-- Interior point: âˆƒ Îµ > 0, B(a, Îµ) âŠ† s.
-- Limit point: âˆ€ Îµ > 0, B(a, Îµ) âˆ© s â‰  âˆ….
-- Boundary point: âˆ€ Îµ > 0, B(a, Îµ) âˆ© s â‰  âˆ… âˆ§ B(a, Îµ) âˆ© sá¶œ â‰  âˆ….

-- A point a is in the closure of a set s iff
-- for every Îµ > 0, there exists a point b in s s.t. a âˆˆ B(b, Îµ)
-- (or, equivalently, âˆ€ Îµ > 0, B(a, Îµ) âˆ© s â‰  âˆ…)
-- `mem_closure_iff`: a âˆˆ closure s â†” âˆ€ Îµ > 0, âˆƒ b âˆˆ s, dist(a, b) < Îµ
-- Intuition: we can always find a point in s arbitrarily close to a.
-- Intuition: a is a limit point of s, so a is either in s or on the boundary of s.
example {a : X} {s : Set X} : a âˆˆ closure s â†” âˆ€ Îµ > 0, âˆƒ b âˆˆ s, a âˆˆ Metric.ball b Îµ :=
  Metric.mem_closure_iff

-- A point is in the closure of a set if
-- there is a sequence in the set that converges to the point.
-- Given
--  a point a
--  a sequence (uâ‚™)
--  a set s
-- If
--  (uâ‚™) converges to a
--  âˆ€ n âˆˆ â„•, uâ‚™ âˆˆ s
-- Then
--  a is in the closure of s
example {u : â„• â†’ X} {s : Set X} (hu : Tendsto u atTop (ğ“ a)) (hs : âˆ€ n, u n âˆˆ s) :
    a âˆˆ closure s :=
  by
  -- By definition of the closure of s,
  -- show âˆ€ Îµ > 0, âˆƒ b âˆˆ s, dist(a, b) < Îµ.
  rw [Metric.mem_closure_iff]
  -- Let Îµ > 0. Show âˆƒ b âˆˆ s, dist(a, b) < Îµ.
  intro Îµ hÎµpos
  -- Since (uâ‚™) converges to a and Îµ > 0,
  -- âˆƒ N âˆˆ â„•, âˆ€ n â‰¥ N, dist(uâ‚™, a) < Îµ.
  rw [Metric.tendsto_atTop] at hu
  -- Fix such N âˆˆ â„•. Then âˆ€ n â‰¥ N, dist(uâ‚™, a) < Îµ.
  rcases (hu Îµ hÎµpos) with âŸ¨N, hâŸ©
  -- Let b = u_N.
  -- Show b âˆˆ s and dist(a, b) < Îµ.
  use u N
  constructor
  Â· -- Show b âˆˆ s.
    -- Since uâ‚™ âˆˆ s âˆ€ n âˆˆ â„•, b = u_N âˆˆ s.
    exact hs N
  Â· -- Show dist(a, b) < Îµ.
    -- Or, equivalently, show dist(u_N, a) < Îµ.
    rw [dist_comm]
    -- Since âˆ€ n â‰¥ N, dist(uâ‚™, a) < Îµ, dist(u_N, a) < Îµ.
    exact h N (le_refl N)

-- Open balls form a basis for the neighborhood filter.
#check Metric.nhds_basis_ball
-- All neighborhoods of a point contain an open ball centered at the point.
-- More formally, a set s is in the neighborhood filter of a point x iff
-- there is a positive radius Îµ s.t. the open ball centered at x with radius Îµ
-- is entirely contained in s.
-- i.e., s âˆˆ ğ“ x â†” âˆƒ Îµ > 0, B(x, Îµ) âŠ† s
example {x : X} {s : Set X} : s âˆˆ ğ“ x â†” âˆƒ Îµ > 0, Metric.ball x Îµ âŠ† s :=
  Metric.nhds_basis_ball.mem_iff

-- Closed balls also form a basis for the neighborhood filter.
#check Metric.nhds_basis_closedBall
-- A set s is in the neighborhood filter of a point x iff
-- there is a positive radius Îµ s.t. the closed ball centered at x with radius Îµ
-- is entirely contained in s.
example {x : X} {s : Set X} : s âˆˆ ğ“ x â†” âˆƒ Îµ > 0, Metric.closedBall x Îµ âŠ† s :=
  Metric.nhds_basis_closedBall.mem_iff

-- A function f is continuous at a point x iff
-- for every neighborhood V of f(x), there exists a neighborhood U of x
-- s.t. f(U) âŠ† V.
-- `mem_map_iff_exists_image`: U âˆˆ f(F) â†” âˆƒ V âˆˆ F, f(V) âŠ† U
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} {x : X} :
  ContinuousAt f x â†” (âˆ€ V âˆˆ ğ“ (f x), âˆƒ U âˆˆ ğ“ x, f '' U âŠ† V) := âŸ¨
    fun h _ hV => mem_map_iff_exists_image.mp (h hV),
    fun h' V hV => mem_map_iff_exists_image.mpr (h' V hV),
  âŸ©

-- A function f is continuous at a point x iff
-- for every Îµ > 0, there exists a Î´ > 0 s.t.
-- f(B(x, Î´)) âŠ† B(f(x), Îµ).
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X â†’ Y} {x : X} :
  ContinuousAt f x â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, f '' Metric.ball x Î´ âŠ† Metric.ball (f x) Îµ :=
  by
  constructor
  Â· -- Suppose f is continuous at x.
    -- Consider Îµ > 0.
    -- Show âˆƒ Î´ > 0, f(B(x, Î´)) âŠ† B(f(x), Îµ).
    intro h Îµ hÎµpos
    -- By definition of continuity at x,
    -- we have f tends to f(x) as input tends to x.
    -- This means that for all neighborhood V of f(x),
    -- fâ»Â¹(V) is a neighborhood of x.
    rw [ContinuousAt, tendsto_def] at h
    -- Since B(f(x), Îµ) is a neighborhood of f(x),
    -- fâ»Â¹(B(f(x), Îµ)) is a neighborhood of x.
    have := h (Metric.ball (f x) Îµ) (Metric.ball_mem_nhds (f x) hÎµpos)
    -- By definition of a neighborhood of x,
    -- âˆƒ Î´ > 0, B(x, Î´) âŠ† fâ»Â¹(B(f(x), Îµ)).
    rw [Metric.mem_nhds_iff] at this
    -- Fix such Î´ > 0. Then, B(x, Î´) âŠ† fâ»Â¹(B(f(x), Îµ)).
    -- Show f(B(x, Î´)) âŠ† B(f(x), Îµ).
    rcases this with âŸ¨Î´, hÎ´pos, hâŸ©
    use Î´, hÎ´pos
    -- Since image and preimage are a Galois connection,
    -- f(B(x, Î´)) âŠ† B(f(x), Îµ).
    exact image_subset_iff.mpr h
  Â· -- Suppose âˆ€ Îµ > 0, âˆƒ Î´ > 0, f(B(x, Î´)) âŠ† B(f(x), Îµ).
    -- Show f is continuous at x.
    intro h
    -- Consider a neighborhood V of f(x).
    -- Show V âˆˆ f(ğ“(x)).
    intro V hV
    -- Since V is a neighborhood of f(x),
    -- âˆƒ Îµ > 0, B(f(x), Îµ) âŠ† V.
    rw [Metric.mem_nhds_iff] at hV
    -- Fix such Îµ > 0. Then B(f(x), Îµ) âŠ† V.
    rcases hV with âŸ¨Îµ, hÎµpos, h'âŸ©
    -- Since Îµ > 0, âˆƒ Î´ > 0 s.t. f(B(x, Î´)) âŠ† B(f(x), Îµ).
    -- Fix such Î´ > 0. Then f(B(x, Î´)) âŠ† B(f(x), Îµ).
    rcases h Îµ hÎµpos with âŸ¨Î´, hÎ´pos, hâŸ©
    -- We can show V âˆˆ f(ğ“(x)) if
    -- we show fâ»Â¹(V) is a neighborhood of x.
    apply mem_map.mpr
    -- This is equivalent to
    -- show âˆƒ Î´ > 0, B(x, Î´) âŠ† fâ»Â¹(V).
    rw [Metric.mem_nhds_iff]
    -- Let Î´ be the same as before.
    -- Show B(x, Î´) âŠ† fâ»Â¹(V).
    use Î´, hÎ´pos
    -- This is equivalent to
    -- Show f(B(x, Î´)) âŠ† V.
    apply image_subset_iff.mp
    -- Since f(B(x, Î´)) âŠ† B(f(x), Îµ) and B(f(x), Îµ) âŠ† V,
    -- f(B(x, Î´)) âŠ† V.
    intro _ hy
    exact h' (h hy)

-- 10.2.3. Compactness

-- Many properties of finite sets
-- can be extended to infinite sets that are "compact".

-- `IsCompact` is a typeclass for compact sets.
-- In Lean, compactness is defined using filters:
-- A set is compact if every non-trivial filter that contains the set
-- has a cluster point in the set.
-- A cluster point is a point whose neighborhoods intersects the filter non-trivially.
example {s : Set X} :
  IsCompact s = âˆ€ F [NeBot F], F â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, ClusterPt x F := rfl

-- The closed unit interval in â„, [0, 1], is a closed set.
example : IsCompact (Set.Icc 0 1 : Set â„) :=
  isCompact_Icc

-- Any sequence taking values in a compact set has
-- a subsequence that converges to a point in the set.
-- Intuition: No sequence can escape a compact set to infinity.
-- Relevant lemma: `IsCompact.tendsto_subseq`
-- Given
--   a set s
--   a sequence (u(n))
-- If
--   s is compact
--   âˆ€ n âˆˆ â„•, u(n) âˆˆ s
-- Then:
--   there exists an a âˆˆ s
--   there exists a subsequence indexed by Ï† : â„• â†’ â„•
--   such that Ï† is strictly increasing
--   and the subsequence ((u âˆ˜ Ï†)(n)) converges to a
example {s : Set X} (hs : IsCompact s) {u : â„• â†’ X} (hu : âˆ€ n, u n âˆˆ s) :
    âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Tendsto (u âˆ˜ Ï†) atTop (ğ“ a) :=
  hs.tendsto_subseq hu

-- Extreme value theorem:
-- Any continuous function on a non-empty compact set with values in â„
-- is bounded and attains its bounds somewhere.
-- Relevant lemma: `IsCompact.exists_isMinOn` and `IsCompact.exists_isMaxOn`
-- Given
--   a set s
--   a function f : X â†’ â„
-- If
--   s is compact and non-empty
--   f is continuous on s
-- Then:
--   there exists a point x âˆˆ s s.t. âˆ€ y âˆˆ s, f(x) â‰¤ f(y)
example {s : Set X} (hs : IsCompact s) (hs' : s.Nonempty) {f : X â†’ â„}
      (hfs : ContinuousOn f s) :
    âˆƒ x âˆˆ s, âˆ€ y âˆˆ s, f x â‰¤ f y :=
  hs.exists_isMinOn hs' hfs
-- Same as above but for the maximum.
example {s : Set X} (hs : IsCompact s) (hs' : s.Nonempty) {f : X â†’ â„}
      (hfs : ContinuousOn f s) :
    âˆƒ x âˆˆ s, âˆ€ y âˆˆ s, f y â‰¤ f x :=
  hs.exists_isMaxOn hs' hfs

-- Compact sets are closed.
-- Relevant lemma: `IsCompact.isClosed`
example {s : Set X} (hs : IsCompact s) : IsClosed s :=
  hs.isClosed

-- A metric space is compact if the entire space is compact.
-- In Lean, `CompactSpace` is a typeclass for compact spaces.

-- In a compact space, the universe set is compact.
example {X : Type*} [MetricSpace X] [CompactSpace X] : IsCompact (univ : Set X) :=
  isCompact_univ

-- In a compact space, any closed set is compact.
#check IsClosed.isCompact

example {X : Type*} [MetricSpace X] {Y : Type*} [MetricSpace Y] {f : X â†’ Y} :
    UniformContinuous f â†”
      âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : X}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ :=
  Metric.uniformContinuous_iff

example {X : Type*} [MetricSpace X] [CompactSpace X]
      {Y : Type*} [MetricSpace Y] {f : X â†’ Y}
    (hf : Continuous f) : UniformContinuous f :=
  by
  rw [Metric.uniformContinuous_iff]
  intro Îµ hÎµpos
  let Ï† : X Ã— X â†’ â„ := fun p â†¦ dist (f p.1) (f p.2)
  let K := { p : X Ã— X | Îµ â‰¤ Ï† p }
  have hÏ† : Continuous Ï† := hf.fst'.dist hf.snd'
  have hK : IsCompact K := (isClosed_le continuous_const hÏ†).isCompact
  rcases eq_empty_or_nonempty K with (hKemp | hKnemp)
  Â· use 1
    constructor
    Â· exact zero_lt_one
    Â· intro a b _
      contrapose! hKemp
      use (a, b)
      rw [mem_setOf]
      exact hKemp
  Â· rcases hK.exists_isMinOn hKnemp continuous_dist.continuousOn with âŸ¨x, hxK, hxinfâŸ©
    use dist x.1 x.2
    constructor
    Â· apply dist_pos.mpr
      intro h
      have : Îµ â‰¤ 0 := by
        calc
          Îµ â‰¤ Ï† x := hxK
          _ = dist (f x.1) (f x.2) := by dsimp [Ï†]
          _ = dist (f x.1) (f x.1) := by rw [â† h]
          _ = 0 := by apply dist_eq_zero.mpr; rfl
      linarith
    Â· intro a b h
      contrapose! h
      rw [isMinOn_iff] at hxinf
      exact hxinf (a, b) h


example (u : â„• â†’ X) :
    CauchySeq u â†” âˆ€ Îµ > 0, âˆƒ N : â„•, âˆ€ m â‰¥ N, âˆ€ n â‰¥ N, dist (u m) (u n) < Îµ :=
  Metric.cauchySeq_iff

example (u : â„• â†’ X) :
    CauchySeq u â†” âˆ€ Îµ > 0, âˆƒ N : â„•, âˆ€ n â‰¥ N, dist (u n) (u N) < Îµ :=
  Metric.cauchySeq_iff'

example [CompleteSpace X] (u : â„• â†’ X) (hu : CauchySeq u) :
    âˆƒ x, Tendsto u atTop (ğ“ x) :=
  cauchySeq_tendsto_of_complete hu

open BigOperators

open Finset

theorem cauchySeq_of_le_geometric_two' {u : â„• â†’ X}
    (hu : âˆ€ n : â„•, dist (u n) (u (n + 1)) â‰¤ (1 / 2) ^ n) : CauchySeq u := by
  rw [Metric.cauchySeq_iff']
  intro Îµ Îµ_pos
  obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, 1 / 2 ^ N * 2 < Îµ := by
    have : Tendsto (fun n : â„• â†¦ (1 / 2 ^ n : â„)) atTop (ğ“ 0) := by
      simp_rw [â† one_div_pow]
      apply tendsto_pow_atTop_nhds_zero_of_lt_one (by linarith) (by linarith)
    rw [Metric.tendsto_atTop] at this
    rcases this (Îµ / 2) (half_pos Îµ_pos) with âŸ¨N, hNâŸ©
    have := hN N (le_refl N)
    use N
    rw [Real.dist_0_eq_abs] at this
    have h : 0 â‰¤ 1 / (2 : â„) ^ N := by
      rw [â† one_div_pow]
      apply pow_nonneg
      simp only [one_div, inv_nonneg, Nat.ofNat_nonneg]
    rw [abs_of_nonneg h] at this
    linarith only [this]
  use N
  intro n hn
  obtain âŸ¨k, rfl : n = N + kâŸ© := le_iff_exists_add.mp hn
  calc
    dist (u (N + k)) (u N) = dist (u (N + 0)) (u (N + k)) := by
      rw [dist_comm]
      rw [add_zero N]
    _ â‰¤ âˆ‘ i in range k, dist (u (N + i)) (u (N + (i + 1))) :=
      dist_le_range_sum_dist (fun i => u (N + i)) k
    _ â‰¤ âˆ‘ i in range k, (1 / 2 : â„) ^ (N + i) :=
      sum_le_sum fun i _ => hu (N + i)
    _ = 1 / 2 ^ N * âˆ‘ i in range k, (1 / 2 : â„) ^ i := by
      simp_rw [â† one_div_pow, pow_add, mul_sum]
    _ â‰¤ 1 / 2 ^ N * 2 := by
      apply mul_le_mul
      Â· apply le_refl
      Â· exact sum_geometric_two_le k
      Â· apply sum_nonneg
        intro _ _
        apply pow_nonneg
        simp
      Â· rw [â† one_div_pow]
        apply pow_nonneg
        simp
    _ < Îµ := hN


open Metric

example [CompleteSpace X] (f : â„• â†’ Set X) (ho : âˆ€ n, IsOpen (f n)) (hd : âˆ€ n, Dense (f n)) :
    Dense (â‹‚ n, f n) := by
  let B : â„• â†’ â„ := fun n â†¦ (1 / 2) ^ n
  have Bpos : âˆ€ n, 0 < B n := fun n => pow_pos (by simp) n
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Î´, Î´pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Î´`.
    We can also require `radius â‰¤ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have :
    âˆ€ (n : â„•) (x : X),
      âˆ€ Î´ > 0, âˆƒ y : X, âˆƒ r > 0, r â‰¤ B (n + 1) âˆ§ closedBall y r âŠ† closedBall x Î´ âˆ© f n :=
    by
    intro n x Î´ hÎ´pos
    have hdn := hd n
    rw [dense_iff] at hdn
    rcases hdn x (Î´ / 2) (half_pos hÎ´pos) with âŸ¨y, hyb, hyfnâŸ©
    have hon := ho n
    rw [Metric.isOpen_iff] at hon
    rcases hon y hyfn with âŸ¨Îµ, hÎµpos, hbyÎµfnâŸ©
    let Îµ' := min (min (Î´ / 2) (B (n + 1))) (Îµ / 2)
    have hÎµ'B : Îµ' â‰¤ Î´ / 2 := le_trans (min_le_left _ _) (min_le_left _ _)
    have hÎµ'Îµ : Îµ' < Îµ := by
      have : Îµ' â‰¤ (Îµ / 2) := min_le_right _ _
      apply lt_of_le_of_lt this
      linarith [hÎµpos]
    use y, Îµ'
    constructor
    Â· exact lt_min (lt_min (half_pos hÎ´pos) (Bpos (n + 1))) (half_pos hÎµpos)
    Â· constructor
      Â· exact le_trans (min_le_left _ _) (min_le_right _ _)
      Â· intro z hz
        constructor
        Â· rw [mem_closedBall] at *
          rw [mem_ball] at hyb
          calc
            dist z x â‰¤ dist z y + dist y x := dist_triangle z y x
            _ â‰¤ Î´ / 2 + Î´ / 2 := add_le_add (le_trans hz hÎµ'B) (le_of_lt hyb)
            _ = Î´ := by ring
        Â· apply hbyÎµfn
          rw [mem_closedBall] at hz
          rw [mem_ball]
          exact lt_of_le_of_lt hz hÎµ'Îµ
  choose! center radius Hpos HB Hball using this
  intro x
  rw [mem_closure_iff_nhds_basis nhds_basis_closedBall]
  intro Îµ Îµpos
  /- `Îµ` is positive. We have to find a point in the ball of radius `Îµ` around `x`
    belonging to all `f n`. For this, we construct inductively a sequence
    `F n = (c n, r n)` such that the closed ball `closedBall (c n) (r n)` is included
    in the previous ball and in `f n`, and such that `r n` is small enough to ensure
    that `c n` is a Cauchy sequence. Then `c n` converges to a limit which belongs
    to all the `f n`. -/
  let F : â„• â†’ X Ã— â„ := fun n â†¦
    Nat.recOn n (Prod.mk x (min Îµ (B 0)))
      fun n p â†¦ Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : â„• â†’ X := fun n â†¦ (F n).1
  let r : â„• â†’ â„ := fun n â†¦ (F n).2
  have rpos : âˆ€ n, 0 < r n := by
    intro n
    induction' n with n hn
    Â· dsimp [r, F]
      exact lt_min Îµpos (Bpos 0)
    Â· exact Hpos n (c n) (r n) hn
  have rB : âˆ€ n, r n â‰¤ B n := by
    intro n
    induction' n with n hn
    Â· dsimp [r, F, B]
      exact min_le_right _ _
    Â· exact HB n (c n) (r n) (rpos n)
  have incl : âˆ€ n, closedBall (c (n + 1)) (r (n + 1)) âŠ† closedBall (c n) (r n) âˆ© f n := by
    intro n
    exact Hball n (c n) (r n) (rpos n)
  have cdist : âˆ€ n, dist (c n) (c (n + 1)) â‰¤ B n := by
    intro n
    have := (rpos (n + 1) |> le_of_lt |> mem_closedBall_self |> incl n).left
    rw [mem_closedBall'] at this
    exact le_trans this (rB n)
  have : CauchySeq c := cauchySeq_of_le_geometric_two' cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with âŸ¨y, ylimâŸ©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y
  have I : âˆ€ n, âˆ€ m â‰¥ n, closedBall (c m) (r m) âŠ† closedBall (c n) (r n) := by
    intro n
    apply Nat.le_induction
    Â· exact subset_refl _
    Â· intro m _ hss
      exact (incl m).trans (Set.inter_subset_left.trans hss)
  have yball : âˆ€ n, y âˆˆ closedBall (c n) (r n) := by
    intro n
    apply isClosed_ball.mem_of_tendsto ylim
    apply (eventually_ge_atTop n).mono
    intro m hnm
    exact rpos m |> le_of_lt |> mem_closedBall_self |> I n m hnm
  constructor
  Â· rw [mem_iInter]
    intro n
    exact ((n + 1) |> yball |> incl n).right
  Â· rw [mem_closedBall]
    have := yball 0
    rw [mem_closedBall] at this
    dsimp [c, r, F] at this
    exact le_trans this (min_le_left _ _)
